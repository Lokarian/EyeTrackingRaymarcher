// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel SideBySide
#pragma kernel LeftEyeOnly
#pragma kernel AntiAliasing
#pragma kernel No3d
#pragma enable_d3d11_debug_symbols

RWTexture2D<float4> textureOut;
uint2 resolution;
float4x4 viewFrustrum;
float3 rightEyePosition;
float3 leftEyePosition;
float normalEpsilon = 0.001f;
float marchEpsilon = 0.001f;
float sphereSize = 2.0f;
float softShadowFactor = 2.0f;
float shadowStartDistance = 2.0f;
int maxIterations = 500000;
int maxReflections = 0;
Texture2D<float4> _Skybox;
SamplerState sampler_Skybox;

float3 lightPosition = float3(10, 10, 10);
float3 lightColor = float3(1, 1, 1);
float ambientFactor = 1;
float shininess = 1;

static const float PI = 3.14159265359f;

const int _OR = 0;
const int _AND = 1;
const int _NOT = 2;
const int _SPHERE = 3;
const int _TORUS = 4;
const int _SMOOTHINTERSECTS = 5;
const int _SMOOTHUNION = 6;
const int _SMOOTHDIFFERENCE = 7;
const int _CUBE = 8;


struct GeneralTreeElement
{
    int nodeType;
    float4x4 translation;
    float4 color;
    float4 a;
    float4 b;
};

struct LinearStackElement
{
    float distance;
    float4 color;
};

//RWStructuredBuffer<LinearStackElement> stackContext;
//LinearStackElement stackContext[25];
//int stackPointer = 0;
StructuredBuffer<GeneralTreeElement> distanceTree;
int distanceTreeLength;

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    // TODO: insert actual code here!

    textureOut[id.xy] = float4(id.x & id.y, (id.x & 15) / 15.0, (id.y & 15) / 15.0, 0.0);
}

bool vecLessThen(float3 a, float3 b)
{
    return a.x * a.x + a.y * a.y + a.z * a.z < b.x * b.x + b.y * b.y + b.z * b.z;
}


LinearStackElement makeLinearStackElement(float distance, float4 color)
{
    LinearStackElement element;
    element.distance = distance;
    element.color = color;
    return element;
}

float4 interpolateColorsWithAlpha(float4 c1,float4 c2)
{
    float interpolationFactor = c1.a / (c1.a + c2.a);
    return float4(lerp(c1.rgb, c2.rgb, interpolationFactor), max(c1.a, c2.a));
}

float logicalAND(float d1, float d2)
{
    return max(d1, d2);
}

LinearStackElement logicalANDWithColor(LinearStackElement c1, LinearStackElement c2)
{
    return makeLinearStackElement(logicalAND(c1.distance, c2.distance), c1.color);
}


float logicalOR(float d1, float d2)
{
    return min(d1, d2);
}

LinearStackElement logicalORWithColor(LinearStackElement c1, LinearStackElement c2)
{
    return makeLinearStackElement(logicalOR(c1.distance, c2.distance), c1.distance < c2.distance ? c1.color : c2.color);
}

float logicalNOT(float d)
{
    return -d;
}

LinearStackElement logicalNOTWithColor(LinearStackElement l)
{
    return makeLinearStackElement(logicalNOT(l.distance), float4(l.color.rgb,0.0f));
}

float getDistanceCirlce(float3 p)
{
    return length(p) - 0.5f;
}

float getDistanceTorus(float3 p, float2 s)
{
    float2 q = float2(length(p.xz) - s.x, p.y);
    return length(q) - s.y;
}

float smoothIntersectSDF(float d1, float d2, float k)
{
    return lerp(d1,d2,k);
    float h = clamp(0.5 - 0.5 * (d1 - d2) / k, 0., 1.);
    return lerp(d1, d2, h) + k * h * (1. - h);
}
 
float2 smoothUnionSDF(float a, float b, float k)
{
    float h = max( k-abs(a-b), 0.0 )/k;
    float m = h*h*0.5;
    float s = m*k*(1.0/2.0);
    return (a<b) ? float2(a-s,m) : float2(b-s,1.0-m);
}

float smoothDifferenceSDF(float d1, float d2, float k)
{
    float h = clamp(0.5 - 0.5 * (d2 + d1) / k, 0., 1.);
    return lerp(d1, -d2, h) + k * h * (1. - h);
}

LinearStackElement logicalsmoothDifferenceWithColor(LinearStackElement c1, LinearStackElement c2, float k)
{
    return makeLinearStackElement(logicalAND(c1.distance, c2.distance), interpolateColorsWithAlpha(c1.color, c2.color));
}
LinearStackElement logicalsmoothIntersecsWithColor(LinearStackElement c1, LinearStackElement c2, float k)
{
    return makeLinearStackElement(logicalAND(c1.distance, c2.distance), interpolateColorsWithAlpha(c1.color, c2.color));
}
LinearStackElement logicalsmoothUnionWithColor(LinearStackElement c1, LinearStackElement c2, float k)
{
    float2 smoothMin = smoothUnionSDF(c1.distance,c2.distance,k);
    return makeLinearStackElement(smoothMin.x,  lerp(c1.color,c2.color,smoothMin.y));
}

float getDistanceCube(float3 p,float3 s)
{
    float3 q = abs(p) - s;
    return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0);
}

float getDistanceCoordinateGrid(float3 p)
{
    float3 q = abs(p) - 0.5;
    return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0);
}

float testCross(float2 a, float2 b, float2 p) {
    return sign((b.y-a.y) * (p.x-a.x) - (b.x-a.x) * (p.y-a.y));
}

// Determine which side we're on (using barycentric parameterization)
float signBezier(float2 A, float2 B, float2 C, float2 p)
{ 
    float2 a = C - A, b = B - A, c = p - A;
    float2 bary = float2(c.x*b.y-b.x*c.y,a.x*c.y-c.x*a.y) / (a.x*b.y-b.x*a.y);
    float2 d = float2(bary.y * 0.5, 0.0) + 1.0 - bary.x - bary.y;
    return lerp(sign(d.x * d.x - d.y), lerp(-1.0, 1.0, 
        step(testCross(A, B, p) * testCross(B, C, p), 0.0)),
        step((d.x - d.y), 0.0)) * testCross(A, C, B);
}

// Solve cubic equation for roots
float3 solveCubic(float a, float b, float c)
{
    float p = b - a*a / 3.0, p3 = p*p*p;
    float q = a * (2.0*a*a - 9.0*b) / 27.0 + c;
    float d = q*q + 4.0*p3 / 27.0;
    float offset = -a / 3.0;
    if(d >= 0.0) { 
        float z = sqrt(d);
        float2 x = (float2(z, -z) - q) / 2.0;
        float2 uv = sign(x)*pow(abs(x), float2(1.0/3.0,1.0/3.0));
        return float3(offset + uv.x + uv.y,offset + uv.x + uv.y,offset + uv.x + uv.y);
    }
    float v = acos(-sqrt(-27.0 / p3) * q / 2.0) / 3.0;
    float m = cos(v), n = sin(v)*1.732050808;
    return float3(m + m, -n - m, n - m) * sqrt(-p / 3.0) + offset;
}

// Find the signed distance from a point to a bezier curve
float sdBezier(float2 A, float2 B, float2 C, float2 p)
{    
    B = lerp(B + float2(1e-4,1e-4), B, abs(sign(B * 2.0 - A - C)));
    const float2 a = B - A, b = A - B * 2.0 + C, c = a * 2.0, d = A - p;
    const float3 k = float3(3.*dot(a,b),2.*dot(a,a)+dot(d,b),dot(d,a)) / dot(b,b);      
    const float3 t = clamp(solveCubic(k.x, k.y, k.z), 0.0, 1.0);
    float2 pos = A + (c + b*t.x)*t.x;
    float dis = length(pos - p);
    pos = A + (c + b*t.y)*t.y;
    dis = min(dis, length(pos - p));
    pos = A + (c + b*t.z)*t.z;
    dis = min(dis, length(pos - p));
    return dis;    // * signBezier(A, B, C, p);  //No need for this sign
}
float sdfBezier(float2 A, float2 B, float2 C, float3 p,float thickness)
{
    return length(float2(sdBezier(A,B,C,p.xy)-thickness,p.z));
}

float3 repeatSpace(float3 p, float3 s)
{
    return (p+s)%(2*s)-sign(p+s)*s;
}

LinearStackElement getDistanceFromTreeCompact(float3 position)
{
    LinearStackElement stackContext[25];
    float3 positionStack[25];
    int positionPointer = 0;
    int stackPointer = 0;
    positionStack[positionPointer]=position;
    for (int i = 0; i < distanceTreeLength; i++)
    {
        const GeneralTreeElement element = distanceTree[i];
        LinearStackElement c1;
        LinearStackElement c2;
        [branch]
        switch (element.nodeType)
        {
            case 0: //_OR
                c1 = stackContext[--stackPointer];
                c2 = stackContext[--stackPointer];
                stackContext[stackPointer++] = logicalORWithColor(c1,c2);
                break;
            case 1: //_AND
                c1 = stackContext[--stackPointer];
                c2 = stackContext[--stackPointer];
                stackContext[stackPointer++] = logicalANDWithColor(c1, c2);
                break;
            case 2: //_NOT
                c1 = stackContext[--stackPointer];
                stackContext[stackPointer++] = logicalNOTWithColor(c1);
                break;
            case 3: //_SPHERE
                stackContext[stackPointer++] = makeLinearStackElement(getDistanceCirlce(mul(element.translation, float4(positionStack[positionPointer], 1.0)).xyz)/element.b.x, element.color);
                break;
            case 4: //_TORUS
                stackContext[stackPointer++] = makeLinearStackElement(getDistanceTorus(mul(element.translation, float4(positionStack[positionPointer], 1.0)).xyz, element.a.xy)/element.b.x, element.color);
                break;
            case 5: //_SMOOTHINTERSECTS
                c1 = stackContext[--stackPointer];
                c2 = stackContext[--stackPointer];
                stackContext[stackPointer++] = logicalsmoothIntersecsWithColor(c1, c2, element.a.x);
                break;
            case 6: //_SMOOTHUNION
                c1 = stackContext[--stackPointer];
                c2 = stackContext[--stackPointer];
                stackContext[stackPointer++] =logicalsmoothUnionWithColor(c1, c2, element.a.x);
                break;
            case 7: //_SMOOTHDIFFERENCE
                c1 = stackContext[--stackPointer];
                c2 = stackContext[--stackPointer];
                stackContext[stackPointer++] = logicalsmoothDifferenceWithColor(c1, c2, element.a.x);
                break;
            case 8: //_CUBE
                stackContext[stackPointer++] = makeLinearStackElement(getDistanceCube(mul(element.translation, float4(positionStack[positionPointer], 1.0)).xyz,element.a)/element.b.x,element.color);
                break;
            case 9: // repetition
                float3 newPos=repeatSpace(positionStack[positionPointer],element.a);
                positionStack[++positionPointer]=newPos;
                break;
            case 10: // undo space
                positionPointer--;
                break;
            case 11: //spline
                stackContext[stackPointer++] = makeLinearStackElement(sdfBezier(element.a.xy,element.a.zw,element.b.yz,mul(element.translation, float4(positionStack[positionPointer], 1.0)).xyz,element.b.w)/element.b.x,element.color);
            break;
             
            default:
                stackContext[stackPointer++] = makeLinearStackElement(0.0f, float4(1.0f, 0.0f, 1.0f, 1.0f));
                break;
        }
    }
    return stackContext[stackPointer - 1];
}
LinearStackElement getDistanceStatic(float3 p){
    return logicalORWithColor(
        makeLinearStackElement(getDistanceCube(mul(float4x4(1,0,0,0 ,0,1,0,1 ,0,0,1,0 ,0,0,0,0),float4(p,1.0)),float3(25,0.1,25)),float4(1,1,1,0.8)),
        logicalORWithColor(
            makeLinearStackElement(getDistanceCirlce(mul(float4x4(1,0,0,1 ,0,1,0,0 ,0,0,1,-1 ,0,0,0,0),float4(p,1.0))),float4(1,0.5,0,0.5)),
            logicalsmoothUnionWithColor(
                makeLinearStackElement(getDistanceTorus(mul(float4x4(1,0,0,-0.75 ,0,0.70711,-0.70711,0.70711 ,0,0.70711,0.70711,-0.70711 ,0,0,0,0),float4(p,1.0)),float2(0.5,0.25f)),float4(0,0.9,0.1,0.5)),
                logicalANDWithColor(
                    makeLinearStackElement(getDistanceCube(mul(float4x4(1,0,0,-1.75 ,0,1,0,0 ,0,0,1,-1 ,0,0,0,0),float4(p,1.0)),float3(0.5f,0.5f,0.5f)),float4(1,0,0,0.5)),
                    logicalNOTWithColor(
                        makeLinearStackElement(getDistanceCirlce(mul(float4x4(1,0,0,-1.75 ,0,1,0,0.5 ,0,0,1,-0.75 ,0,0,0,0),float4(p,1.0))),float4(0,0,1,0.5))
                    )
                ),
                0.25f
            )
        )
        );
}
float getDistance(float3 p)
{
    //return getDistanceStatic(p).distance;
    return getDistanceFromTreeCompact(p).distance;
}
LinearStackElement getDistanceAndMaterialColor(float3 p)
{
    //return getDistanceStatic(p);
    return getDistanceFromTreeCompact(p);
}

float3 reflectRay(float3 ray, float3 surfaceNormal)
{
    return ray - 2.0f * dot(ray, surfaceNormal) * surfaceNormal;
}
float getAngleBetweenNormalized(float3 ray1, float3 ray2)
{
    return acos(dot(ray1, ray2));
}

float3 getSurfaceNormal(float3 p)
{
    return normalize(getDistance(p) -
        float3(
            getDistance(p - float3(marchEpsilon, 0, 0)),
            getDistance(p - float3(0, marchEpsilon, 0)),
            getDistance(p - float3(0, 0, marchEpsilon))
        ));
}

float3 sampleSkybox(float3 direction)
{
    float theta = acos(direction.y) / -PI;
    float phi = atan2(direction.x, direction.z) / -PI * 0.5f;
    return _Skybox.SampleLevel(sampler_Skybox, float2(phi, theta), 0);
}

float3 calculatePhongColor(float3 materialColor,float3 surfaceNormal,float3 viewDirection,float3 toLightDirection,float illumination)
{
    float3 colorOut = materialColor.rgb * ambientFactor; //ambient
    colorOut += materialColor.rgb * lightColor * max(0.0f, dot(surfaceNormal, toLightDirection)) * illumination; //diffuse
    float3 halfwayVector = normalize((-viewDirection + toLightDirection) / 2.0f);
    colorOut += materialColor.rgb * lightColor * max(0.0f, ceil(dot(surfaceNormal, toLightDirection))) * pow(max(0.0f, dot(surfaceNormal, halfwayVector)), shininess) * illumination; //specular
    return colorOut;
}

float4 AddColorWithReflectiveness(float4 colorIn, float3 colorToAdd,float reflectivness){
    float ownPortion = (1.0f - colorIn.a) * (1.0f - reflectivness);
    return float4(colorIn.rgb+colorToAdd*ownPortion,colorIn.a+ownPortion);
}

float4 CastRay(float3 origin, float3 direction)
{
    float3 marchPosition = origin;
    float3 marchDirection = normalize(direction);
    float3 hitPoint;
    float3 hitDirection;
    LinearStackElement hitContext;
    float3 hitSurfaceNormal;
    int iterations = 0;
    int reflections=0;
    float smallestDistance = 1000000.0f;
    int marchState=0;
    float penumbraFactor = 100000000.0f;
    float4 colorOut = float4(0,0,0,0);
    do
    {
        LinearStackElement context = getDistanceAndMaterialColor(marchPosition);
        float distance = context.distance;
        if (distance < marchEpsilon)
        {
                const float3 hitSurfaceNormal=getSurfaceNormal(marchPosition);
                const float3 color = calculatePhongColor(context.color, hitSurfaceNormal, marchDirection, lightPosition-marchPosition, 0.1f);
                colorOut = AddColorWithReflectiveness(colorOut, color,context.color.a);
                if (++reflections > maxReflections||context.color.a<0.001f)
                {
                    return float4(colorOut.rgb,iterations);
                }
                marchDirection = reflect(marchDirection,hitSurfaceNormal);
            marchPosition +=hitSurfaceNormal*marchEpsilon;
        }     
        marchPosition += (marchDirection * distance);
        if (smallestDistance > distance)
        {
            smallestDistance = distance;
        }
        if (iterations > maxIterations)
        {
            return float4(1.0, 0.0, 1.0, 1.0);
        }
        if (length(marchPosition) > 1000.0f)
        {
            return float4(AddColorWithReflectiveness(colorOut, sampleSkybox(marchDirection),0.0f).rgb,iterations);
        }
        iterations++;
    }
    while (true);
    return float4(direction, 1.0);
}
float DoLighting(float3 origin)
{
    float3 marchPosition = origin;
    float3 direction = getDistanceAndMaterialColor(marchPosition).distance;
    for(int i=0; i<maxIterations;i++)
    {
        float distance =getDistance(marchPosition);
        if(length(lightPosition-origin)<length(marchPosition-origin))
        {
            return 1;
        }
        if(distance<marchEpsilon)
        {
            return 0;
        }
        marchPosition+=direction*distance;
    }
    return 0.0f;
}

struct positionAndColor
{
    float4 color;
    float3 position;
};
positionAndColor CastRay2(float3 origin, float3 direction)
{
    float3 marchPosition = origin;
    for(int i=0; i<maxIterations;i++)
    {
        LinearStackElement context = getDistanceAndMaterialColor(marchPosition);
        if(context.distance<marchEpsilon)
        {
            float lightLevel=DoLighting(marchPosition);
            float3 color = calculatePhongColor(context.color, getSurfaceNormal(marchPosition), direction, lightPosition-marchPosition, lightLevel);
            positionAndColor outs;
            outs.color=float4(context.color.rgb,1.0);
            outs.position=marchPosition;
            return outs;
            
        }
        marchPosition+=context.distance*direction;
    }
    positionAndColor outs;
    outs.color=float4(1,0,1,1.0);
    outs.position=marchPosition;
    return outs;
}
float4 CastRay2WithReflection(float3 origin, float3 direction,int reflections)
{
    float rayOrig=origin;
    float rayDir=direction;
    float4 colorOut = float4(0,0,0,0);
    for(int i=0;i<=reflections;i++)
    {
        positionAndColor tes = CastRay2(rayOrig,rayDir);
        return tes.color;
        colorOut=AddColorWithReflectiveness(colorOut,tes.color.rgb,tes.color.a);
        rayOrig=tes.position;
        rayDir= reflect(rayDir,getSurfaceNormal(rayOrig));
        
    }
    return colorOut;
}
float3 calculateViewRay(float2 pixelCoordinate, float isRightEye)
{
    return mul(viewFrustrum, float4(pixelCoordinate.x, pixelCoordinate.y, 1.0f, isRightEye)).xyz;
}


[numthreads(8, 4, 1)]
void SideBySide(uint3 id : SV_DispatchThreadID)
{
    uint2 pixelCoordinate = id.xy;
    float3 eyePosition;
    float isRightEye;
    if (pixelCoordinate.x < resolution.x / 2)
    {
        eyePosition = leftEyePosition;
        isRightEye = 0.0f;
    }
    else
    {
        eyePosition = rightEyePosition;
        isRightEye = 1.0f;
    }
    float2 uv = float2(pixelCoordinate.x % (resolution.x / 2) / (float) (resolution.x / 2),
                                       pixelCoordinate.y / (float) resolution.y);
    const float3 directionRay = mul(viewFrustrum, float4(uv.x, uv.y, 1.0f, isRightEye)).xyz;
    const float4 color = CastRay(eyePosition, directionRay);
    textureOut[id.xy] = color;
}


[numthreads(8, 4, 1)]
void LeftEyeOnly(uint3 id : SV_DispatchThreadID)
{
    uint2 pixelCoordinate = id.xy;
    float2 realtiveCoordinate = float2(pixelCoordinate.x / (float) resolution.x,
                                       pixelCoordinate.y / (float) resolution.y);
    float3 directionRay = calculateViewRay(realtiveCoordinate, 0.0f);
    float4 color = CastRay(leftEyePosition, directionRay);
    if(all(pixelCoordinate<resolution))
    {
        textureOut[id.xy] = color;        
    }
}
[numthreads(8, 4, 1)]
void No3d(uint3 id : SV_DispatchThreadID)
{
    uint2 pixelCoordinate = id.xy;
    float2 realtiveCoordinate = float2(pixelCoordinate.x / (float) resolution.x,
                                       pixelCoordinate.y / (float) resolution.y);
    float3 directionRay = calculateViewRay(realtiveCoordinate, 0.0f);
    float4 color = lerp(float4(0,0,0,0),float4(1,1,1,0),CastRay(leftEyePosition, directionRay).a/maxIterations);
    if(all(pixelCoordinate<resolution))
    {
        textureOut[id.xy] = color;        
    }
}

[numthreads(8, 4, 1)]
void AntiAliasing(uint3 id : SV_DispatchThreadID)
{
    float4 color;
    for (int i = 0; i < 4; i++)
    {
        for (int j = 0; j < 4; j++)
        {
            uint2 pixelCoordinate = id.xy;
            float2 realtiveCoordinate = float2((pixelCoordinate.x + 0.25f * i - 0.375f) / (float) resolution.x,
                                       (pixelCoordinate.y + 0.25f * j - 0.375f) / (float) resolution.y);
            float3 directionRay = calculateViewRay(realtiveCoordinate, 0.0f);
            color += CastRay(leftEyePosition, directionRay);
        }
    }

    textureOut[id.xy] = color/16;
}
